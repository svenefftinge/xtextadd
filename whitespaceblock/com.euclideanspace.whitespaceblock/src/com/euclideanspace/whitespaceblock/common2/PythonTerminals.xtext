grammar com.euclideanspace.whitespaceblock.PythonTerminals hidden(WS, SL_COMMENT)
							
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

/*
 * These terminals are intended to allow indents (whitespace) is used to delineate
 * blocks (Python like syntax).
 * 
 * Since this is difficult to do directly in Xtext we will add code to insert
 * 'phantom tokens' to be used in the grammar like curly braces in java.
 * These phantom tokens are not in the text but are inserted automatically when
 * the indent increases or decreases.
 * 
 * We should not use 'Curly Braces' for these phantom tokens, because they are used in
 * Python to define a data structure called a dictionary, so we should change the code
 * below
 */
  
terminal ID  		: '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
terminal INT returns ecore::EInt: ('0'..'9')+;
terminal STRING	: 
			'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"' |
			"'" ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|"'") )* "'"
		; 

/* phantom tokens:
 * use something other than '{' and '}' to avoid clash in Python so we use '{|' and '|}' 
 * TODO Should really use something more obscure to avoid clash in any user language
 */
terminal BEGIN : '{|';
terminal END : '|}';

/* if newline follows this then ignore indent changes */
terminal LINECONTINUATION : '\\';

/*
 * Comments start with hash.
 * We need to specify that here because we want to allow comments with any indent
 * and to avoid them being wrapped in a block.
 */
terminal SL_COMMENT 	: '#' !('\n'|'\r')* ('\r'? '\n')?;

/*
 * We also don't want lines that contain only whitespace to affect the indent.
 */
terminal WS : (' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER: .;